#! /usr/local/bin/perl6
use Test;

class Shortcut {
    has Str $.type;
}

class Quant {
    has $.atom;
}

role Grammar {
    token grammar {
        'grammar' <.ws> '{'
            <.ws>
            <rule>*
        '}'
        <.ws>

        {
            die "There must be a 'TOP' rule defined"
                unless 'TOP' ∈ $<rule>».ast»<name>;
            make $<rule>».ast;
        }
    }

    token rule {
        'token' <.ws> <ident> <.ws> '{'
            <.ws>
            $<contents>=[
                <quanted-atom>*
            ]
        ['}' || <panic("Expected regex atom or '}'")>]
        <.ws>

        {
            make {
                name => ~$<ident>,
                contents => $<contents>.Str.trim,
                atoms => $<quanted-atom>».ast,
            };
        }
    }

    token quanted-atom {
        <atom>
        <.ws>
        [<quant> <.ws>]?

        {
            my $atom = $<atom>.ast;

            make $<quant>
                ?? Quant.new(:$atom)
                !! $atom;
        }
    }

    proto token atom {*}

    token atom:literal {
        <[']>
            $<literal>=(<-[']>*)
        <[']>
        <.ws>

        { make ~$<literal> }
    }

    token atom:character-class {
        '<['
            <charclass-element>*
        ']>'
        <.ws>

        { make (my Range @ = $<charclass-element>».ast) }
    }

    token atom:charclass-shortcut {
        '\\'
        $<type>=<[wdhsWDHS]>
        <.ws>

        { make Shortcut.new(type => ~$<type>) }
    }

    proto token charclass-element {*}

    token charclass-element:char {
        <-[\]]>

        { make Range.new(~$/, ~$/) }
    }

    token charclass-element:range {
        $<from>=<-[\]]>
        '..'
        $<to>=<-[\]]>

        { make Range.new(~$<from>, ~$<to>) }
    }

    token quant {
        '?'
    }

    method panic($) { ... }
}

role Spec {
    token spec {
        <section>*
        { make $<section>».ast }
    }

    token section {
        <heading> \n
        [<.empty-line> | <section-line>]*
        {
            make {
                heading => $<heading>.ast,
                lines => $<section-line>».ast,
            };
        }
    }

    token heading {
        ^^ '##' \h* $<heading>=(\N+) \n
        {
            my $heading = ~$<heading>;
            die "Sorry, unrecognized heading `$heading`"
                unless $heading eq "Happy parse" | "Sad parse";
            make ~$<heading>;
        }
    }

    token empty-line {
        ^^ \h* \n
    }

    token section-line {
        ^^ <[']> $<line>=(<-[']>*) <[']> \n
        { make $<line>.Str.trans(['\\f', '\\n', '\\r', '\\t'] => ["\f", "\n", "\r", "\t"]) }
    }
}

grammar GrammarSpec does Grammar does Spec {
    token TOP {
        <.ws>
        <grammar>
        <spec>

        {
            make {
                grammar => $<grammar>.ast,
                spec => $<spec>.ast,
            }
        }
    }

    method panic($message) {
        my $pos = self.pos;
        my $orig = self.orig;

        sub de-nl($s) {
            $s.subst(/\n/, "\x[2424]", :g);
        }

        my $before-length = 12;
        my $before = de-nl $orig.substr( ($pos - $before-length max 0) ..^ $pos );
        my $before-ellipsis = $pos - $before-length > 0
            ?? "..."
            !! "";

        my $snippet-length = 12;
        my $snippet = de-nl $orig.substr($pos, $snippet-length);
        my $snippet-ellipsis = $pos < $orig.chars - $snippet-length
            ?? "..."
            !! "";

        my $some-text = "$before-ellipsis$before⏏$snippet$snippet-ellipsis";
        note "Parse failed: $message (`$some-text` at pos $pos)";
        exit 1;
    }
}

class State {
    has Int $.id;
    has Bool $.goal = False;
    has @.transitions;

    method add-transition-to(State :$next-state!, :$matcher!, Bool :$negated = False) {
        @.transitions.push(transition(:$next-state, :$matcher, :$negated));
    }

    method mark-as-goal() {
        $!goal = True;
    }
}

sub escape($s) {
    $s.trans(["\f", "\n", "\r", "\t"] => ['\\f', '\\n', '\\r', '\\t']);
}

role Transition {
    has State $.next-state;

    method matches(Str $char) { ... }
}

class Transition::Char does Transition {
    has Str $.char;

    method Str { qq["{escape($.char)}"] }
    method matches(Str $char) { $char eq $.char }
}

class Transition::Ranges does Transition {
    has Range @.ranges;
    has Bool $.negated;

    sub format($r) {
        $r.min eq $r.max
            ?? qq["{escape($r.min)}"]
            !! qq[{escape($r.min)}..{escape($r.max)}];
    }
    method Str { @.ranges.map(&format).join(", ") }
    method matches(Str $char) { $.negated ^^ so $char ~~ any(@.ranges) }
}

multi transition(State :$next-state!, Str :$matcher!, :$negated) {
    Transition::Char.new(:$next-state, char => $matcher);
}

multi transition(State :$next-state!, Range :@matcher!, Bool :$negated = False) {
    Transition::Ranges.new(:$next-state, ranges => @matcher, :$negated);
}

multi transition(State :$next-state!, :$matcher!, :$negated) {
    die "Unrecognized charclass element type: ", $matcher.^name;
}

class NFA {
    has Str $.name;
    has Str $.contents;
    has State @.states;
    has Set[State] $.start-states;
    has State $!current-state;
    has Int $!next-unique-id;

    submethod BUILD(:$!name!, :$!contents!) {
        $!next-unique-id = 0;
        my $start-state = self!add-state();
        $!start-states = Set[State].new($start-state);
        $!current-state = $start-state;
    }

    method !add-state() {
        my $state = State.new(id => $!next-unique-id++);
        @!states.push($state);
        return $state;
    }

    method add-state-with-transition($matcher, Bool :$negated) {
        my $next-state = self!add-state();
        if $matcher !~~ Array[Range] || +$matcher || $negated { # no need to add an empty transition
            $!current-state.add-transition-to(:$next-state, :$matcher, :$negated);
        }
        $!current-state = $next-state;
        return;
    }

    method mark-current-state-as-goal() {
        $!current-state.mark-as-goal();
        return;
    }

    method with-bypass(&cb) {
        my $before-state = $!current-state;
        &cb();
        self!inherit-transitions($before-state, $!current-state);
    }

    method !inherit-transitions($s1, $s2) {
        if $s1 ∈ $!start-states {
            $!start-states ∪= $s2;
        }
        else {
            # XXX
            die "We don't yet handle the case of inheriting the transitions to a non-start state";
        }
    }
}

sub dump-nfa(NFA $nfa) {
    constant $INDENT = " " x 4;
    say "# NFA for token ", $nfa.name, " \{ ", $nfa.contents, " \} ";
    for $nfa.states -> $state {
        say "# ",
            $INDENT,
            ($state ∈ $nfa.start-states ?? "(start) " !! ""),
            "STATE ", $state.id,
            ($state.goal ?? " (goal)" !! "");

        for $state.transitions -> $t {
            my $arrow = "---[{$t.Str}]--->";

            say "# {$INDENT x 2}{$arrow} {$t.next-state.id}";
        }
    }
    say "";
}

sub run-nfa(NFA $nfa, Str $input) {
    my $current-states = $nfa.start-states;
    for $input.comb -> $char {
        my $next-states = set();
        for $current-states.keys -> State $state {
            for $state.transitions -> $t {
                if $t.matches($char) {
                    $next-states ∪= set($t.next-state);
                }
            }
        }
        $current-states = $next-states;
    }
    return ?any($current-states.keys».goal);
}

my (:$grammar, :$spec) := GrammarSpec.parse(slurp()).ast;
my @rules = @$grammar;
my %nfa-for-rule;

for @rules -> $rule {
    my $name = $rule<name>;
    my $contents = $rule<contents>;
    my $nfa = NFA.new(:$name, :$contents);
    for @($rule<atoms>) -> $atom {
        multi add-atom(Str $literal) {
            for $literal.comb -> $char {
                $nfa.add-state-with-transition($char);
            }
        }

        multi add-atom(Array[Range] $charclass) {
            $nfa.add-state-with-transition($atom);
        }

        multi add-atom(Shortcut $shortcut) {
            sub r($c) { $c..$c }
            my Range @elements = do given $shortcut.type.lc {
                when "d" { ("0".."9",) }
                when "w" { "0".."9", "A".."Z", "a".."z", r("_") }
                when "s" { r("\x[20]"), "\x[09]".."\x[0D]" }
                when "h" { r("\x[20]"), r("\x[09]") }
                default { die "Unknown type: ", $atom.type }
            };
            my $negated = so $shortcut.type ~~ /<+upper>/;
            $nfa.add-state-with-transition(@elements, :$negated);
        }

        multi add-atom(Quant $quant) {
            $nfa.with-bypass: {
                add-atom($quant.atom);
            }
        }

        multi add-atom(Any $unknown-atom) {
            die "Unrecognized atom type: ", $unknown-atom.^name;
        }

        add-atom($atom);
    }
    $nfa.mark-current-state-as-goal();
    dump-nfa($nfa);
    %nfa-for-rule{$name} = $nfa;
}
my $top-nfa = %nfa-for-rule<TOP>;

my @sections = @$spec;
for @sections -> $section {
    my $heading = $section<heading>;
    my $expected-result = $heading eq "Happy parse";
    my $expectation = $expected-result
        ?? "should succeed"
        !! "should fail";
    for @($section<lines>) -> $line {
        my $escaped-line = escape($line);
        ok run-nfa($top-nfa, $line) == $expected-result, "$heading: parsing '$escaped-line' $expectation";
    }
}

done-testing;
