#!/usr/bin/env perl6
use v6;
use Alma;
use Alma::Backend::JavaScript;

class Ref {
    has Str $.deref;
}

sub ref($deref) {
    Ref.new(:$deref);
}

constant %BACKENDS = hash
    "default" => ref("runtime"),
    "runtime" => { $^runtime.run($^ast) },
    "javascript" => -> $ast, $ { print Alma::Backend::JavaScript.new.emit($ast) },
    "js" => ref("javascript"),
    "ast" => -> $ast, $ { say ~$ast },
    "unexpanded-ast" => -> $ast, $ { say ~$ast },
;

constant $REPL_PROGRAM = q:to/./;
    use Alma;
    my $runtime = Alma.runtime;

    while defined my $program = prompt "> " {
        my $ast = Alma.parser(:$runtime).parse($program);
        $runtime.run($ast);
        CATCH {
            default {
                .say;
            }
        }
    }
    .

sub run-alma($program, Str $backend is copy, @arguments) {
    die "Unknown backend '$backend'"
        unless %BACKENDS{$backend} :exists;
    $backend = %BACKENDS{$backend}.deref
        while %BACKENDS{$backend} ~~ Ref;

    my $runtime = Alma.runtime(:@arguments);
    my $unexpanded = $backend eq "unexpanded-ast";
    my $ast = Alma.parser(:$runtime).parse($program, :$unexpanded);
    %BACKENDS{$backend}($ast, $runtime);
    exit($runtime.exit-code);
}

multi MAIN(Str :$backend = "default") {
    my $rlwrap-installed = "" ne qx[which rlwrap].trim;
    if !$rlwrap-installed {
        note "rlwrap not found in PATH. (Do you have it installed?)";
        exit(1);
    }

    shell "rlwrap perl6 -e '{$REPL_PROGRAM}'";
}

multi MAIN($path, Str :$backend = "default", *@arguments) {
    run-alma(slurp($path), $backend, @arguments);
}

multi MAIN(Str :e($program)!, Str :$backend = "default", *@arguments) {
    run-alma($program, $backend, @arguments);
}
